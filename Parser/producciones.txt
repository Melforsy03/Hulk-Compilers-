NonTerminals:
Program, Type_function_list, Expr_block, Expr_list, Expr_item_list, Expr, Or_expr, And_expr, Aritm_comp,
Concat, Arithmetic, Term, Pow, Factor, Conditional, Cond_other_case, Let_expr, Assignment, Destr_assig,
Atom, Call_func, Member, Variable, Params, Method_signature, Inline_form, Full_form, Func, Arguments,
While_loop, For_loop, Cast_type, Check_type, Sign, Type_block, Type_member_list, Type_member_item,
Type, Type_dec, Type_inst, Protocol_block, Method_dec_list, Protocol, Vector, Vector_exp, Vector_imp,
Vector_item_list, Index_object

Terminals:
{, }, ;, (, ), ,, :, ., :=, =>, [, ], ||, |, &, ==, !=, >, >=, <, <=, !, =,
@, @@, +, -, *, /, ^, **, %, bool, id, num, let, in, function, string, if,
elif, else, as, is, while, for, type, inherits, new, protocol, extends

StartSymbol: Program

Productions:
Program -> Type_function_list Expr_item_list
Type_function_list -> Func Type_function_list
Type_function_list -> Type Type_function_list
Type_function_list -> Protocol Type_function_list
Type_function_list -> ε

Params -> id , Params
Params -> id
Params -> id : id , Params
Params -> id : id

Method_signature -> id ( Params ) : id
Method_signature -> id ( Params )
Method_signature -> id ( ) : id
Method_signature -> id ( )

Inline_form -> Method_signature => Expr ;

Full_form -> Method_signature Expr_block ;
Full_form -> Method_signature Expr_block

Func -> function Inline_form
Func -> function Full_form

Type -> type Type_dec Type_block
Type -> type Type_dec inherits id Type_block
Type -> type Type_dec inherits id ( Arguments ) Type_block

Type_dec -> id ( Params )
Type_dec -> id

Type_block -> { Type_member_list }

Type_member_list -> Type_member_item Type_member_list
Type_member_list -> ε

Type_member_item -> Inline_form
Type_member_item -> Full_form
Type_member_item -> id = Expr ;
Type_member_item -> id : id = Expr ;

Protocol -> protocol id extends id Protocol_block
Protocol -> protocol id Protocol_block

Protocol_block -> { Method_dec_list }

Method_dec_list -> Method_signature ; Method_dec_list
Method_dec_list -> ε

Expr -> Conditional
Expr -> Let_expr
Expr -> While_loop
Expr -> For_loop
Expr -> Destr_assig

Conditional -> if ( Expr ) Expr Cond_other_case
Cond_other_case -> elif ( Expr ) Expr Cond_other_case
Cond_other_case -> else Expr

Let_expr -> let Assignment in Expr

Assignment -> id = Expr , Assignment
Assignment -> id = Expr
Assignment -> id : id = Expr , Assignment
Assignment -> id : id = Expr

While_loop -> while ( Expr ) Expr
For_loop -> for ( id in Expr ) Expr

Destr_assig -> id := Expr
Destr_assig -> Atom . id := Expr
Destr_assig -> Or_expr

Or_expr -> Or_expr | And_expr
Or_expr -> And_expr

And_expr -> And_expr & Check_type
And_expr -> Check_type

Check_type -> Check_type is id
Check_type -> Aritm_comp

Aritm_comp -> Aritm_comp == Concat
Aritm_comp -> Aritm_comp != Concat
Aritm_comp -> Aritm_comp > Concat
Aritm_comp -> Aritm_comp >= Concat
Aritm_comp -> Aritm_comp < Concat
Aritm_comp -> Aritm_comp <= Concat
Aritm_comp -> Concat

Concat -> Concat @ Arithmetic
Concat -> Concat @@ Arithmetic
Concat -> Arithmetic

Arithmetic -> Arithmetic + Term
Arithmetic -> Arithmetic - Term
Arithmetic -> Term

Term -> Term * Pow
Term -> Term / Pow
Term -> Term % Pow
Term -> Pow

Pow -> Sign ^ Pow
Pow -> Sign ** Pow
Pow -> Sign

Sign -> + Factor
Sign -> - Factor
Sign -> Factor

Factor -> ! Atom
Factor -> Atom

Atom -> ( Expr )
Atom -> Expr_block
Atom -> bool
Atom -> Call_func
Atom -> Type_inst
Atom -> Vector
Atom -> Index_object
Atom -> id
Atom -> Member
Atom -> num
Atom -> string
Atom -> Cast_type

Expr_block -> { Expr_list }

Expr_list -> Expr_item_list Expr_list
Expr_list -> Expr_item_list

Expr_item_list -> Expr ;

Call_func -> id ( Arguments )
Call_func -> id ( )

Arguments -> Expr , Arguments
Arguments -> Expr

Type_inst -> new Call_func

Vector -> Vector_exp
Vector -> Vector_imp

Vector_exp -> [ Vector_item_list ]
Vector_exp -> [ ]

Vector_item_list -> Expr , Vector_item_list
Vector_item_list -> Expr

Vector_imp -> [ Expr || id in Expr ]

Index_object -> Atom [ Expr ]

Member -> Atom . Call_func
Member -> Atom . id

Cast_type -> Atom as id